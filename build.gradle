plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'net.researchgate.release' version '2.6.0'
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

group = 'com.abusix.util'
version = '0.1.0-SNAPSHOT'

repositories {
    jcenter()
}

test {
	useJUnitPlatform()
}

dependencies {
    testCompile('org.junit.jupiter:junit-jupiter-api:5.4.1')
    testRuntime('org.junit.jupiter:junit-jupiter-engine:5.4.1')
    api('ch.qos.logback:logback-core:1.2.3') 
    api('ch.qos.logback:logback-classic:1.2.3')
    implementation('com.google.guava:guava:27.1-jre')
}

jar {
    from 'LICENSE'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
    from 'LICENSE'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
    from 'LICENSE'
}

artifacts {
    archives javadocJar, sourcesJar
}

publishing {
    publications {
         mavenJava(MavenPublication) {
            pom {
                from components.java
                artifact sourcesJar
                artifact javadocJar
                name = 'Rate-Limiting Logback Filter'
                description = 'Can be used to rate limit log events based on logger name and top of stack/message (e.g. for sentry.io)'
                url = 'https://github.com/abusix/rate-limiting-logback-filter'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/abusix/rate-limiting-logback-filter/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'FrederikP'
                        name = 'Frederik Petersen'
                        email = 'fp@abusix.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/abusix/rate-limiting-logback-filter.git'
                    developerConnection = 'scm:git:ssh://git@github.com:abusix/rate-limiting-logback-filter.git'
                    url = 'https://github.com/abusix/rate-limiting-logback-filter'
                }
            }
        }
    }

    repositories {
        maven {
            name = 'Central'
            def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username project.ossrhUsername
                password project.ossrhPassword
            }
        }
        maven {
            name = 'Jfrog'
            def releasesRepoUrl = 'https://abusix.jfrog.io/abusix/libs-release-local'
            def snapshotsRepoUrl = 'https://abusix.jfrog.io/abusix/libs-snapshot-local'
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username project.jfrogUsername
                password project.jfrogPassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}